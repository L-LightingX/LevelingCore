plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '8.1.0'
    id 'com.gradleup.shadow' version '9.3.0'
    id 'hytale-mod' version '0.+'
}

configurations {
    shadowBundle
    implementation.extendsFrom(shadowBundle)
    vineflowerTool
}

javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

group = project.group

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
}

repositories {
    mavenCentral()
    maven {
        name = "Hytale Server Release"
        url = uri("https://maven.hytale.com/release")
    }
    maven {
        name = "Hytale Server Pre-Release"
        url = uri("https://maven.hytale.com/pre-release")
    }
    maven {
        name = "hMReleases"
        url = uri("https://maven.hytale-mods.dev/releases")
    }
}

dependencies {
    // Hytale server
    //compileOnly("com.hypixel.hytale:Server:2026.01.22-6f8bdbdc4")
    vineflowerTool("org.vineflower:vineflower:1.11.2")
    compileOnly("com.buuz135:MultipleHUD:1.0.3")
    runtimeOnly("com.buuz135:MultipleHUD:1.0.3")
    compileOnly fileTree(dir: "$rootDir/libs", include: ["*.jar"])
    runtimeOnly fileTree(dir: "$rootDir/libs", include: ["*.jar"])
    // Needed libs
    shadowBundle 'com.h2database:h2:2.4.240'
    shadowBundle 'com.zaxxer:HikariCP:7.0.2'
    shadowBundle 'org.slf4j:slf4j-simple:2.0.17'
    shadowBundle 'org.yaml:snakeyaml:2.5'
    shadowBundle 'net.objecthunter:exp4j:0.4.8'
    // Drivers
    shadowBundle 'com.mysql:mysql-connector-j:9.5.0'
    shadowBundle 'org.mariadb.jdbc:mariadb-java-client:3.5.7'
    shadowBundle 'org.postgresql:postgresql:42.7.8'
    // Testing
    testImplementation platform("org.junit:junit-bom:6.0.1")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

spotless {
    java {
        eclipse().configFile("$rootDir/eclipse-formatter.xml")
        endWithNewline()
        importOrder("", "java", group.toString(), "\\#")
        leadingTabsToSpaces(4)
        removeUnusedImports()
        trimTrailingWhitespace()
    }
}

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()

    configurations = [project.configurations.shadowBundle]

    manifest {
        attributes(
                'Main-Class': 'com.azuredoom.levelingcore.LevelingCore'
        )
    }
}

tasks.build {
    dependsOn("spotlessApply", "shadowJar")
}

publishing {
    repositories {
        maven {
            name = project.mod_id
            url = project.maven_url
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
}

tasks.register("decompileLibsWithVineflower") {
    group = "decompilation"
    description = "Decompile every .jar in /libs using Vineflower into build/vineflower/<jarName>/"

    def libsDir = file("$rootDir/libs")
    def outDir = file("$buildDir/vineflower")

    inputs.dir(libsDir)
    outputs.dir(outDir)

    doLast {
        if (!libsDir.exists()) {
            logger.lifecycle("No /libs directory found at: ${libsDir}")
            return
        }

        outDir.mkdirs()

        def vineflowerJar = configurations.vineflowerTool.singleFile

        def javaExe = javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(java_version)
        }.get().executablePath.asFile.absolutePath

        def jars = fileTree(dir: libsDir, include: ["*.jar"]).files.toList()
        if (jars.isEmpty()) {
            logger.lifecycle("No .jar files found in ${libsDir}")
            return
        }

        jars.each { jarFile ->
            def jarBase = jarFile.name.replaceAll(/\.jar$/, "")
            def jarOut = new File(outDir, jarBase)
            jarOut.mkdirs()

            def externals = jars.findAll { it != jarFile }.collectMany { ["-e", it.absolutePath] }

            def cmd = [
                    javaExe,
                    "-jar", vineflowerJar.absolutePath,
                    *externals,
                    jarFile.absolutePath,
                    jarOut.absolutePath
            ]

            logger.lifecycle("Running: ${cmd.join(' ')}")

            def pb = new ProcessBuilder(cmd)
            pb.directory(project.projectDir)
            pb.redirectErrorStream(true)

            def proc = pb.start()
            proc.inputStream.withReader { r ->
                r.eachLine { line -> logger.lifecycle(line) }
            }

            logger.lifecycle("Decompiled ${jarFile.name} -> ${jarOut}")
        }
    }
}

def keyPropsFile = new File(rootProject.projectDir, "key.properties")

if (keyPropsFile.exists()) {
    tasks.register("publishToModtale", Exec) {
        group = "publishing"
        description = "Publishes the built jar and changelog to Modtale.net."
        dependsOn tasks.named("build")
        def curlExe = org.gradle.internal.os.OperatingSystem.current().isWindows()
                ? "C:/Windows/System32/curl.exe"
                : "curl"

        doFirst {
            def releaseProp = new Properties()
            releaseProp.load(new FileInputStream(keyPropsFile))
            def apiKey = releaseProp.getProperty("modTaleKey")
            def projectId = releaseProp.getProperty("modTaleProjectId")
            def jarFile = layout.buildDirectory
                    .file("libs/${project.name}-${project.version}.jar")
                    .get().asFile
            def changelogFile = rootProject.file("changelog.md")
            def changelogText = changelogFile.text

            commandLine(
                    curlExe, '-X', 'POST',
                    "https://api.modtale.net/api/v1/projects/$projectId/versions",
                    '-H', "X-MODTALE-KEY: ${apiKey}",
                    '-F', "file=@${jarFile.absolutePath}",
                    '-F', "versionNumber=${project.version}",
                    '-F', "channel=${project.release_type}",
                    '-F', "gameVersions=${project.hytale_version}",
                    '-F', "changelog=${changelogText}"
            )
        }
    }
    // TODO: Move to hytale.curseforge.com when actually working because CurseForge didn't bother getting this ready dispute early access to Hytale.
    tasks.register("publishToCurseForge", Exec) {
        group = "publishing"
        description = "Publishes the built jar and changelog to CurseForge."
        dependsOn tasks.named("build")

        def curlExe = org.gradle.internal.os.OperatingSystem.current().isWindows()
                ? "C:/Windows/System32/curl.exe"
                : "curl"

        doFirst {
            def releaseProp = new Properties()
            releaseProp.load(new FileInputStream(keyPropsFile))
            def apiKey = releaseProp.getProperty("curseKey")
            def projectId = releaseProp.getProperty("curseProjectId")

            def jarFile = layout.buildDirectory
                    .file("libs/${project.name}-${project.version}.jar")
                    .get().asFile

            def changelogText = rootProject.file("changelog.md").text
            def cfReleaseType = project.release_type.toString().toLowerCase()

            def requiredDeps = [
                    "multiplehud"
            ]

            def optionalDeps = [
                    "simpleparty",
                    "partypro",
                    "party-info",
                    "better-modlist"
            ]

            def dependencies = []

            requiredDeps.each {
                dependencies << [ slug: it, type: "requiredDependency" ]
            }
            optionalDeps.each {
                dependencies << [ slug: it, type: "optionalDependency" ]
            }

            def metadataObj = [
                    changelog     : changelogText,
                    changelogType : "markdown",
                    displayName   : "${project.name} ${project.version}",
                    gameVersions  : [14284],
                    releaseType   : cfReleaseType,
                    relations     : [ projects: dependencies ]
            ]
            def metadataJson = groovy.json.JsonOutput.toJson(metadataObj)

            commandLine(
                    curlExe,
                    "-f", "-sS",
                    "-X", "POST",
                    "https://legacy.curseforge.com/api/projects/${projectId}/upload-file",
                    "-H", "X-Api-Token: ${apiKey}",
                    "-F", "metadata=${metadataJson};type=application/json",
                    "-F", "file=@${jarFile.absolutePath}"
            )
        }
    }
}